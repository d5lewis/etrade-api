plugins {
    id 'java'
    id 'application'
    id 'nu.studer.credentials' version '2.1'
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    // Dependencies pull in from old maven project
    implementation 'javax.inject:javax.inject:1'
    implementation 'com.googlecode.json-simple:json-simple:1.1'
    implementation 'commons-cli:commons-cli:1.3.1'
    implementation 'com.google.code.gson:gson:2.8.0'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-xml:2.4.5'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.4.5'
    implementation 'org.codehaus.jackson:jackson-core-asl:1.9.13'
    implementation 'org.apache.commons:commons-lang3:3.0'
    implementation 'org.apache.velocity:velocity:1.7'
    implementation 'org.apache.velocity:velocity-tools:2.0'
    implementation 'commons-collections:commons-collections:3.1'
    implementation 'org.springframework:spring-context:4.2.0.RELEASE'
    implementation 'org.springframework:spring-web:4.2.0.RELEASE'
    implementation 'org.springframework:spring-core:4.2.0.RELEASE'
    implementation 'org.springframework.security.oauth:spring-security-oauth:2.3.5.RELEASE'
    implementation 'org.springframework:spring-expression:4.2.0.RELEASE'
    implementation 'org.apache.httpcomponents:httpclient:4.5'
    implementation 'commons-codec:commons-codec:1.11'

    // Build dependencies
    implementation('org.slf4j:slf4j-api:1.7.25')
    implementation('org.slf4j:slf4j-simple:1.7.25')
    implementation(group: 'javax.servlet', name: 'javax.servlet-api', version: '3.1.0')

    // Test dependencies
    testImplementation 'junit:junit:3.8.1'
    testImplementation 'org.slf4j:slf4j-log4j12:1.7.25'
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

application {
    mainClassName = 'com.wolfml.etrade.api.terminal.ETClientApp'
}

run {
    standardInput = System.in
}

// Set the credentials for oauth
processResources {
    filesMatching('oauth.properties') {
        expand(consumerKey: credentials.consumerKey,
                consumerSecret: credentials.consumerSecret,
                sandboxConsumerKey: credentials.sandboxConsumerKey,
                sandboxConsumerSecret: credentials.sandboxConsumerSecret)
    }
}

// Application information
group = 'com.wolfml.etrade'
version = '0.0.1-SNAPSHOT'
description = 'This application is built to run as a microservice to manage an etrade account from a trading platform.'
